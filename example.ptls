import "binaries.ptls" as bin
import "unaries.ptls" as unary

isEmpty(list) = list == Empty
head(list) = list.!getHead
tail(list) = list.!getTail

take(n, list) =
  if n < 1 then []
  else if isEmpty(list) then []
  else [head(list)] ++ take(n - 1, tail(list))

infinityFrom(n) = [n] ++ infinityFrom(unary.inc(n))

reduce(func, acc, list) =
  if isEmpty(list) then acc
  else reduce(func, func(acc, head(list)), tail(list))

filter(func, list) =
  if isEmpty(list) then []

  else if func(head(list))
    then [head(list)] ++ filter(func, tail(list))

  else filter(func, tail(list))

map(func, list) =
  if isEmpty(list) then []
  else [func(head(list))] ++ map(func, tail(list))

concatMap(func, lists) = lists |> map(func) |> concat
concat(lists) =
  if isEmpty(lists) then []
  else head(lists) ++ concat(tail(lists))

sum(list) = reduce(bin.add, 0, list)

multiply(list) = reduce(bin.mul, 1, list)

range(init, count) = infinityFrom(init)
                     |> take(count)

output = [
          (for i in range(1, 4) yield i)
          |> filter(n => n!=3)
          |> map(n => n**2)
          |> sum
          |> range(1)
          |> multiply
        ]